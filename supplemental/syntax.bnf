# A -> B | B A                A -> B+
# A -> . | B A                A -> B*
# A -> B X | C X | D X        A -> (B | C | D) X
# A -> B | B C                A -> B C?
# A -> B* . B -> CD           A -> (CD)*

<prog> ::= ( <stmt> )* EOF .

# Statements
<stmt> ::= <decl_stmt> .
<body> ::= INDENT ( <stmt> )+ DEDENT .

# Declaration statements
<decl_stmt> ::=
      <var_decl>
    | <fun_decl>
    | <cmpd_stmt> .

<var_decl> ::= "no" IDENTIFIER ( "=" <expr> )? NEWLINE
<fun_decl> ::= "greg" IDENTIFIER "(" ( <params> )? ")" NEWLINE <body> .
<params> ::= IDENTIFIER ( "," IDENTIFIER )* .

# Compound statements
<cmpd_stmt> ::=
      <if_stmt>
    | <while_loop>
    | <bounce_loop>
    | <std_stmt> .

<if_stmt> ::= "if" <expr> NEWLINE <body> ( "erm" <expr> NEWLINE <body> )* ( "hermph" NEWLINE <body> )? .
<while_loop> ::= "while" <expr> NEWLINE <body> ( "hermph" NEWLINE <body> )? .
<bounce_loop> ::= "bounce" NEWLINE <body> "while" <expr> NEWLINE
# <for_loop> ::= "for" IDENTIFIER "in" <expr> NEWLINE <body> ( "hermph" NEWLINE <body> )? .

# Standard statements
<std_stmt> ::= ( <return_stmt> | <expr_stmt> ) NEWLINE .
<return_stmt> ::= "pay" ( <expr> )? .
<expr_stmt> ::= <expr> .

# Operator precedence
# Op                Symbol          Precedence
# assignment          =               lowest
# logical or          or                1
# logical and        and                2
# logical not        not                3
# relational      <,>,<=,>=             4
# equalities        ==,!=               5
# addition           +,-                6
# multiplication    *,/,%               7
# unary              +,-, //            8
# exponentiation     **               highest

<expr> ::= <assign_expr> .
<args> ::= <expr> ( "," <expr> )* .

<assign_expr> ::= <primary> "=" <assign_expr> | <or_expr> .

<or_expr>  ::= <and_expr> ( "or"  <and_expr> )* .
<and_expr> ::= <not_expr> ( "and" <not_expr> )* .
<not_expr> ::= "not" <not_expr> | <eq_expr> .  # allow "not not ... True"

<eq_expr>  ::= <rel_expr> ( ( "==" | "!=" ) <not_expr> )* .
<rel_expr> ::= <add_expr> ( ( "<" | ">" | "<=" | ">=" ) <add_expr> )* .

<add_expr> ::= <mul_expr> ( ( "+" | "-" ) <mul_expr> )* .
<mul_expr> ::= <sign_expr> ( ( "*" | "/" | "%" ) <sign_expr> )* .
<sign_expr> ::= ( "+" | "-" ) <sign_expr> | <exp_expr> .
<squirt_expr> ::= <exp_expr> ( "//" )? .
<exp_expr> ::= <primary> ( "**" <sign_expr> )? .

# Primary expressions
<primary> ::= <atom> ( "(" ( <args> )? ")" | "[" <expr> "]" )* .

<atom> ::=
      INT_LITERAL
    | FLOAT_LITERAL
    | STR_LITERAL
    | IDENTIFIER
    | "True"
    | "False"
    | "nol"
    | <array_initializer>
    | "(" <expr> ")" .

<array_initializer> ::= "[" ( <args> )? "]" .
